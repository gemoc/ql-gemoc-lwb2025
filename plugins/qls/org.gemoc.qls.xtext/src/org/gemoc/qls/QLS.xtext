grammar org.gemoc.qls.QLS with org.gemoc.ql.QL
import "http://www.gemoc.org/ql" as ql
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
//import "http://www.gemoc.org/qls"
import "platform:/resource/org.gemoc.qls.model/model/qls.ecore"
//generate qLS "http://www.gemoc.org/qls/QLS"

QLSModel returns QLSModel:
	{QLSModel}
	imports+=Import*
	'QLSModel'
	styledQLModel=[ql::QLModel|QualifiedName]
	'{'
		'questionStyles' '{'  (questionStyles+=QuestionStyle)* '}' 
		(sections+=Section)*
	'}';

Import returns Import:
	{Import}
	'import' importURI=STRING 
	;
	
Section returns Section:
	{Section}
	'section' title=STRING '{'
		(sectionContents+=SectionContent)*
		 
	'}'
;

SectionContent returns SectionContent:
	Section | QuestionReference
;

QuestionReference returns QuestionReference:
	{QuestionReference}
	question=[ql::QuestionDefinition|QualifiedName]
;

TypeStyle returns TypeStyle:
	BooleanTypeStyle | NumericTypeStyle | TextTypeStyle;


NumericTypeStyle returns NumericTypeStyle:
	NumericTypeTextFieldStyle | NumericTypeSpinnerStyle
;

QuestionStyle returns QuestionStyle:
	'styledQuestion' styledQuestion=[ql::QuestionDefinition|QualifiedName]
	'{'
		('labelStyle' labelStyle=LabelStyle)?
		('typeStyle' typeStyle=TypeStyle)?
	'}';


LabelStyle returns LabelStyle:
	{LabelStyle}
	'{'
		(italic?='italic')?
		(bold?='bold')?
	'}'
	;

BooleanTypeStyle returns BooleanTypeStyle:
	'BooleanStyle'
	'{'
		booleanStyleKind=BooleanStyleKind
	'}';

NumericTypeTextFieldStyle returns NumericTypeTextFieldStyle:
	{NumericTypeTextFieldStyle} 
	'NumericStyle' 'textField'
	'{'	'}';

NumericTypeSpinnerStyle returns NumericTypeSpinnerStyle:
	{NumericTypeSpinnerStyle}
	'NumericStyle' 'spinner' '{'
		('step' step=EDouble)?'}'
	;

TextTypeStyle returns TextTypeStyle:
	{TextTypeStyle}
	
	'TextStyle' '{' (multiline?='multiline')? '}'
	;

enum BooleanStyleKind returns BooleanStyleKind:
				CHECKBOX = 'CHECKBOX' | TWO_RADIO = 'TWO_RADIO' | DROPDOWN = 'DROPDOWN';


